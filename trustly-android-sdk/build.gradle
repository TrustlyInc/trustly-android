plugins {
    id 'maven-publish'
    id 'com.android.library' version '7.4.2'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

android {
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    namespace 'net.trustly.android.sdk'

    compileSdk 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.powermock:powermock-api-mockito:1.4.12'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.5'

    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:truth:1.5.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'
}

tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/BrowserPopup.java'
    exclude '**/UrlUtils.java'
    exclude '**/TrustlyPanel.java'
}

apply from: 'publish-root.gradle'

ext {
    PUBLISH_GROUP_ID = 'net.trustly'
    PUBLISH_VERSION = '3.1.0'
    PUBLISH_ARTIFACT_ID = 'android-sdk'
}

apply from: 'publish-remote.gradle'
