plugins {
    id 'maven-publish'
    id 'com.android.library' version '8.6.1'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'org.jetbrains.kotlin.android' version '1.9.24'
}

def sdkVersion = "3.3.1"
def localIP = "10.0.2.2"

android {
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    namespace 'net.trustly.android.sdk'

    compileSdk 35

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled true
        }
    }

    buildTypes.configureEach {
        buildConfigField "String", "SDK_VERSION", "\"$sdkVersion\""
        buildConfigField "String", "LOCAL_IP", "\"$localIP\""
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildFeatures {
        buildConfig true
    }
    sourceSets {
        debug {
            root 'src/mock'
        }
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.code.gson:gson:2.11.0'
    testImplementation 'org.mockito:mockito-core:5.14.0'
    testImplementation 'org.mockito:mockito-inline:4.2.0'

    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.ext:truth:1.6.0'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
}

tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

apply from: 'publish-root.gradle'

ext {
    PUBLISH_GROUP_ID = 'net.trustly'
    PUBLISH_VERSION = sdkVersion
    PUBLISH_ARTIFACT_ID = 'trustly-android-sdk'
}

apply from: 'publish-remote.gradle'
