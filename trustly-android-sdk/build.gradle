plugins {
    id 'maven-publish'
    id 'com.android.library' version '8.11.0'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'org.jetbrains.kotlin.android' version '2.2.0'
}

def sdkVersion = "3.5.0"
def localIP = "10.0.2.2"

android {
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    namespace = 'net.trustly.android.sdk'

    compileSdk = 35

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled = true
        }
    }

    buildTypes.configureEach {
        buildConfigField "String", "SDK_VERSION", "\"$sdkVersion\""
        buildConfigField "String", "LOCAL_IP", "\"$localIP\""
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildFeatures {
        buildConfig = true
    }
    sourceSets {
        debug {
            root = 'src/mock'
        }
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation 'androidx.browser:browser:1.8.0'
    //noinspection GradleDependency
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'com.google.code.gson:gson:2.11.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.code.gson:gson:2.11.0'
    testImplementation 'org.mockito:mockito-core:5.15.2'
    testImplementation 'org.mockito:mockito-inline:5.2.0'

    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'org.mockito:mockito-android:5.10.0'
}

tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

apply from: 'publish-root.gradle'

ext {
    PUBLISH_GROUP_ID = 'net.trustly'
    PUBLISH_VERSION = sdkVersion
    PUBLISH_ARTIFACT_ID = 'trustly-android-sdk'
}

apply from: 'publish-remote.gradle'
