plugins {
    id 'maven-publish'
    id 'com.android.library' version '8.2.2'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

def sdkVersion = "3.2.2"

android {
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    namespace 'net.trustly.android.sdk'

    compileSdk 34

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", "SDK_VERSION", "\"$sdkVersion\""
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SDK_VERSION", "\"$sdkVersion\""
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.browser:browser:1.8.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.mockito:mockito-inline:4.2.0'

    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
}

tasks.register('javadoc', Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/BrowserPopup.java'
    exclude '**/UrlUtils.java'
}

apply from: 'publish-root.gradle'

ext {
    PUBLISH_GROUP_ID = 'net.trustly'
    PUBLISH_VERSION = sdkVersion
    PUBLISH_ARTIFACT_ID = 'trustly-android-sdk'
}

apply from: 'publish-remote.gradle'
