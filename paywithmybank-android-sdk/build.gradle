plugins {
    id 'maven-publish'
    id 'com.android.library'
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.browser:browser:1.4.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.powermock:powermock-api-mockito:1.4.12'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.2'

    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:truth:1.5.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/BrowserPopup.java'
    exclude '**/UrlUtils.java'
    exclude '**/PayWithMyBankPanel.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        sdk(MavenPublication) {
            artifactId 'paywithmybank-android-sdk'
            artifact("$buildDir/outputs/aar/paywithmybank-android-sdk-release.aar")
            artifact(javadocJar)
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified" == it.name) && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

ext {
    PUBLISH_GROUP_ID = 'io.github.TrustlyInc'
    PUBLISH_VERSION = '2.2.1'
    PUBLISH_ARTIFACT_ID = 'paywithmybank-android-sdk'
}

apply from: 'publish-remote.gradle'